set(dirstxt ${CMAKE_CURRENT_LIST_DIR}/build/dirs.txt)

file(STRINGS ${dirstxt} lis)

foreach(e IN LISTS lis)
    if("${e}" MATCHES "([^:]*): (.*)")
        set(varname ${CMAKE_MATCH_1})
        set(val ${CMAKE_MATCH_2})
        if(DEFINED ${varname})
            list(APPEND ${varname} ${val})
        else()
            set(${varname} ${val})
        endif()
    elseif(e)
        message(FATAL_ERROR "Unknown line: ${e}")
    endif()
endforeach()

# Collect files on the paths
set(total)
foreach(prefix IN LISTS _CPATH _CXXPATH)
    file(GLOB_RECURSE files LIST_DIRECTORIES false
        ${prefix}/* ${prefix}/*.*)
    foreach(e IN LISTS files)
        if(NOT file_${e})
            list(APPEND total ${e})
            set(file_${e} UNUSED)
        endif()
    endforeach()
endforeach()

set(index ${_localbuild}/index.txt)

file(STRINGS ${index} lis)

foreach(e IN LISTS lis)
    if("${e}" MATCHES "([^\t]*)\t(.*)")
        set(sym ${CMAKE_MATCH_1})
        # message(STATUS "sym: ${sym}")
        foreach(lang c cxx)
            set(deps  ${_localbuild}/check/${sym}.${lang}.d)
            if(EXISTS ${deps})
                file(STRINGS ${deps} deps)
                foreach(d IN LISTS deps)
                    if(d)
                        if(file_${d})
                            if("${file_${d}}" STREQUAL UNUSED)
                                set(file_${d} ${sym}:${lang})
                            else()
                                list(APPEND file_${d} ${sym}:${lang})
                            endif()
                        endif()
                    endif()
                endforeach()
            endif()
        endforeach()
    elseif(e)
        message(FATAL_ERROR "Unknown index line: ${e}")
    endif()
endforeach()

foreach(e IN LISTS total)
    if("${file_${e}}" STREQUAL UNUSED)
        message(STATUS "${e}: UNUSED")
    else()
        message(STATUS "${e}")
    endif()
endforeach()
